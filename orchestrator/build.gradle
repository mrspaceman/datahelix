/*
 * Copyright 2019 Scott Logic Ltd
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java"
    id "application"
    id "de.gliderpilot.semantic-release" version "1.4.0"
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(":profile")
    compile project(":core")
    compile project(":output")
    compile project(":common")
    compile project(":custom")

    compile group: "info.picocli", name: "picocli", version: "${PICOCLI_VERSION}"
    compile group: "com.google.code.gson", name: "gson", version: "${GSON_VERSION}"

    testImplementation project(":common").sourceSets.test.output
    testImplementation "org.hamcrest:java-hamcrest:${HAMCREST_VERSION}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${JUNIT_JUPITER_VERSION}"
    testImplementation "org.junit.platform:junit-platform-runner:${JUNIT_PLATFORM_RUNNER_VERSION}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${JUNIT_JUPITER_VERSION}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${JUNIT_JUPITER_VERSION}"
    testImplementation "io.cucumber:cucumber-core:${CUCUMBER_VERSION}"
    testImplementation "io.cucumber:cucumber-junit:${CUCUMBER_VERSION}"
    testImplementation "io.cucumber:cucumber-java:${CUCUMBER_VERSION}"
    testImplementation "io.cucumber:cucumber-jvm:${CUCUMBER_VERSION}"
    testImplementation "io.cucumber:gherkin:${GHERKIN_VERSION}"
    testImplementation "io.cucumber:cucumber-expressions:${CUCUMBER_EXPRESSIONS_VERSION}"
    testImplementation "info.cukes:cucumber-picocontainer:${CUCUMBER_PICOCONTAINER_VERSION}"
    testImplementation "org.mockito:mockito-all:${MOCKITO_VERSION}"
    testImplementation "com.shazam:shazamcrest:${SHAZAMCREST_VERSION}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${JUNIT_JUPITER_VERSION}"
    testImplementation group: "junit", name: "junit", version: "${JUNIT_4_VERSION}"

    testImplementation project(':core').sourceSets.test.output

    testImplementation("org.junit.jupiter:junit-jupiter:${JUNIT_JUPITER_VERSION}")
}

test {
    useJUnitPlatform()
    dependsOn("fatJar")
}

description = "DataHelix Generator"

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.scottlogic.datahelix.generator.orchestrator.App'
    }
    archiveBaseName = 'datahelix'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    archiveFileName = "${archiveBaseName}.${archiveExtension}"
}

task releaseZip(type: Zip) {
    dependsOn("fatJar")

    archiveFileName = "generator.zip"

    from("$buildDir/libs") {
        include "generator.jar"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.scottlogic.datahelix.generator.orchestrator.App'
    }
}

application {
    mainClassName = 'com.scottlogic.datahelix.generator.orchestrator.App'
}

project.ext.ghToken = project.hasProperty('ghToken') ? project.getProperty('ghToken') : System.getenv('GH_TOKEN') ?: null

distZip {
    archiveName "datahelix.zip"
}

startScripts {
    applicationName = 'datahelix'
}

semanticRelease {
    repo {
        ghToken = project.ghToken
        releaseAsset fatJar
    }
}
